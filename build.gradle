apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'docker-compose'
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.8.13"
    }
}


group 'com.swa.sqs'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: '1.11.584'
    compile group: 'org.apache.camel', name: 'camel-aws', version: '2.24.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

dockerCompose {

    useComposeFiles = ["./docker-compose.yml"]
    startedServices = ['localstack']
    forceRecreate = false
    buildBeforeUp = true
    buildBeforePull = true
    ignorePushFailure = false
    buildAdditionalArgs = ['--force-rm']

    //logging
    captureContainersOutput = false
    captureContainersOutputToFile = "${buildDir.path}/logs/all_container_logs.txt"
    //separate logs for all the containers
    containerLogToDir = project.file("${buildDir.path}/logs")
    captureContainersOutputToFiles = "${buildDir.path}/logs/separated"

    waitForTcpPorts = true

    removeContainers = true
    removeVolumes = true
    removeOrphans = true
}

test {
    testLogging.events(TestLogEvent.STANDARD_OUT)
    doFirst{
        def sqsPort = getPublicPortOfContainer("localstack", 4576)
        println(sqsPort)
        systemProperty 'SQS_PORT', sqsPort
    }
}


def getPublicPortOfContainer(containerName, containerPort){
    def dockerPortCommand = "docker port %s %s"
    def command = String.format(dockerPortCommand, containerName, containerPort)
    def result = command.execute().text.trim()
    return result.substring(result.indexOf(":") + 1)
}


test.dependsOn composeUp
test.finalizedBy composeDown
